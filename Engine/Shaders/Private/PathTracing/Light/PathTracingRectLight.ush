// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================================
	RectLight.usf: Light sampling functions for Rect light implementation
===============================================================================================*/

#pragma once

#include "../../RectLight.ush"
#include "PathTracingLightCommon.ush"

#if USE_RECT_LIGHT_TEXTURES

// The number of entries must match PATHTRACER_MAX_RECT_TEXTURES
Texture2D RectLightTexture_0;
Texture2D RectLightTexture_1;
Texture2D RectLightTexture_2;
Texture2D RectLightTexture_3;
Texture2D RectLightTexture_4;
Texture2D RectLightTexture_5;
Texture2D RectLightTexture_6;
Texture2D RectLightTexture_7;
SamplerState RectLightSampler;

float3 EvaluateTexture(int LightId, float2 UV)
{
	// TODO: could use path roughness / ray cone to lower the mip level here
	UV.y = 1 - UV.y; // match orientation with raster path
	switch (SceneLights[LightId].RectLightTextureIndex)
	{
		case 0:  return RectLightTexture_0.SampleLevel(RectLightSampler, UV, 0).xyz;
		case 1:  return RectLightTexture_1.SampleLevel(RectLightSampler, UV, 0).xyz;
		case 2:  return RectLightTexture_2.SampleLevel(RectLightSampler, UV, 0).xyz;
		case 3:  return RectLightTexture_3.SampleLevel(RectLightSampler, UV, 0).xyz;
		case 4:  return RectLightTexture_4.SampleLevel(RectLightSampler, UV, 0).xyz;
		case 5:  return RectLightTexture_5.SampleLevel(RectLightSampler, UV, 0).xyz;
		case 6:  return RectLightTexture_6.SampleLevel(RectLightSampler, UV, 0).xyz;
		case 7:  return RectLightTexture_7.SampleLevel(RectLightSampler, UV, 0).xyz;
		default: return 1;
	}
}

#endif


float3 BilinearQuadWarp(float2 uv, float W00, float W01, float W10, float W11) {
	// "Practical Product Sampling by Fitting and Composing Warps" - EGSR 2020
	// https://casual-effects.com/research/Hart2020Sampling/index.html
	// https://www.shadertoy.com/view/wljyDz

	float a = lerp(W00, W01, .5);
	float b = lerp(W10, W11, .5);
	float u = a == b ? uv.x : (sqrt(lerp(a * a, b * b, uv.x)) - a) / (b - a);
	float c = lerp(W00, W10, u);
	float d = lerp(W01, W11, u);
	float v = c == d ? uv.y : (sqrt(lerp(c * c, d * d, uv.y)) - c) / (d - c);
	float area = lerp(a, b, .5);
	float pdf = lerp(c, d, v) / area;
	return float3(u, v, pdf);
}

// #dxr_todo: This method should be moved to RectLight.ush
// #dxr_todo: Analyze the overall efficiency (MSE reduction vs. time)
//  Projected solid angle sampling converges faster at equal sample count, but is a bit slower
// #dxr_todo: Should we switch to plain area sampling at some distance?
float4 SampleApproxProjectionSphericalRect(float2 Rand, FSphericalRect SphericalRect, float3 WorldNormal)
{
	// Construct the quad vertices
	float3 S00 = float3(SphericalRect.x0, SphericalRect.y0, SphericalRect.z0);
	float3 S10 = float3(SphericalRect.x1, SphericalRect.y0, SphericalRect.z0);
	float3 S11 = float3(SphericalRect.x1, SphericalRect.y1, SphericalRect.z0);
	float3 S01 = float3(SphericalRect.x0, SphericalRect.y1, SphericalRect.z0);

	// Compute the cosine of the normal against each corner of the quad
	float3 LightSpaceNormal = mul(SphericalRect.Axis, WorldNormal);
	float W00 = saturate(dot(normalize(S00), LightSpaceNormal));
	float W10 = saturate(dot(normalize(S10), LightSpaceNormal));
	float W11 = saturate(dot(normalize(S11), LightSpaceNormal));
	float W01 = saturate(dot(normalize(S01), LightSpaceNormal));

	// Warp a 2D sample according to the cosine at each corner of the quad
	float3 WarpedRand = BilinearQuadWarp(Rand, W00, W01, W10, W11);

	// Proceed with uniform sampling of the spherical rectangle
	float3 Direction = UniformSampleSphericalRect(WarpedRand.xy, SphericalRect);
	float OutPdf = isfinite(SphericalRect.SolidAngle) ? 1.0 / SphericalRect.SolidAngle : 0.0;

	// Return Direction vector and adjusted PDF
	return float4(Direction, OutPdf * WarpedRand.z);
}

FLightHit RectLight_TraceLight(RayDesc Ray, int LightId)
{
	float3 LightPosition = GetPosition(LightId);
	float3 LightNormal = GetNormal(LightId);
	float3 LightDirection = LightPosition - Ray.Origin;
	float DoN = dot(Ray.Direction, LightNormal);
	float t = dot(LightDirection, LightNormal) / DoN;
	// ray points toward the plane and intersect it?
	if (DoN < 0 && t > Ray.TMin && t < Ray.TMax)
	{
		float3 LightdPdu = GetdPdu(LightId);
		float3 LightdPdv = GetdPdv(LightId);
		float2 LightExtent = 0.5 * GetRectSize(LightId);

		float3 P = t * Ray.Direction - LightDirection;
		float2 UV = float2(dot(P, LightdPdu), dot(P, LightdPdv));
		// test point against 
		if (all(abs(UV) <= LightExtent))
		{
			// Clip the Rectangle by the barndoors
			FRect Rect = GetRect(LightDirection,
				-LightNormal,
				LightdPdv,
				LightExtent.x,
				LightExtent.y,
				GetRectLightBarnCosAngle(LightId),
				GetRectLightBarnLength(LightId),
				true);
			P = t * Ray.Direction - Rect.Origin;
			// test again with the clipped extents
			float2 ClippedUV = float2(dot(P, LightdPdu), dot(P, LightdPdv));
			if (all(abs(ClippedUV) <= Rect.Extent))
			{
				// stored color is radiance
				float3 Radiance = GetColor(LightId) * ComputeIESAttenuation(LightId, Ray.Origin);
				Radiance *= ComputeAttenuationFalloff(dot(LightDirection, LightDirection), LightId);
				float SolidAngle = BuildSphericalRect(Rect).SolidAngle;
#if USE_RECT_LIGHT_TEXTURES
				Radiance *= EvaluateTexture(LightId, (UV + LightExtent) / (LightExtent * 2));
#endif
				return CreateLightHit(Radiance, 1.0 / SolidAngle, t);
			}
		}
	}
	return NullLightHit();
}

FLightSample RectLight_SampleLight(
	int LightId,
	float2 RandSample,
	float3 WorldPos,
	float3 WorldNormal
)
{
	float3 LightPosition = GetPosition(LightId);
	float3 LightNormal = GetNormal(LightId);
	float3 LightdPdu = GetdPdu(LightId);
	float3 LightdPdv = GetdPdv(LightId);
	float LightWidth = GetWidth(LightId);
	float LightHeight = GetHeight(LightId);

	// Define rectangle and compute solid angle
	float3 LightDirection = LightPosition - WorldPos;
	FRect Rect = GetRect(LightDirection,
		-LightNormal,
		LightdPdv,
		0.5 * LightWidth,
		0.5 * LightHeight,
		GetRectLightBarnCosAngle(LightId),
		GetRectLightBarnLength(LightId),
		true /* bComputeVisibleRect */);
	if (!IsRectVisible(Rect) || dot(Rect.Axis[2], Rect.Origin) < 0)
	{
		return NullLightSample();
	}
	FSphericalRect SphericalRect = BuildSphericalRect(Rect);

#if USE_RECT_LIGHT_TEXTURES
	FSphericalRectSample Sample = UniformSampleSphericalRectWithUV(RandSample, SphericalRect);
	float2 ClippedUV = Sample.UV;
	float3 Direction = Sample.Direction;
#else
	float3 Direction = UniformSampleSphericalRect(RandSample, SphericalRect);
#endif

	float3 RadianceOverPdf = GetColor(LightId) * SphericalRect.SolidAngle * ComputeIESAttenuation(LightId, WorldPos);
	RadianceOverPdf *= ComputeAttenuationFalloff(dot(LightDirection, LightDirection), LightId);
#if USE_RECT_LIGHT_TEXTURES
	float2 UV = 0.5 * ((2 * ClippedUV - 1) * Rect.Extent + Rect.Offset) / Rect.FullExtent + 0.5;
	RadianceOverPdf *= EvaluateTexture(LightId, UV);
#endif

	return CreateLightSample(RadianceOverPdf, 1.0 / SphericalRect.SolidAngle, normalize(Direction), length(Direction));
}

float RectLight_EstimateLight(
	int LightId,
	float3 WorldPos,
	float3 WorldNormal,
	bool IsTransmissiveMaterial
)
{
	// Distance to centroid
	// #dxr_todo: UE-72533 Use closest point, instead
	float3 LightDirection = GetPosition(LightId) - WorldPos;
	float LightDistanceSquared = dot(LightDirection, LightDirection);
	float3 LightNormal = GetNormal(LightId);

	// Is the shading point behind the light?
	float LNoL = saturate(-dot(normalize(LightDirection), LightNormal));
	if (LNoL <= 0.0)
	{
		return 0.0;
	}
	// Approximate geometric term
	float Width = GetWidth(LightId);
	float Height = GetHeight(LightId);

	// Don't bother trying to bound the N.L term as its in [0,1] and hard to estimate accurately and quickly
	float NoL = 1.0;
	float Area = Width * Height;

	float LightPower = Luminance(GetColor(LightId));
	float Falloff = ComputeAttenuationFalloff(LightDistanceSquared, LightId);
	return LightPower * Falloff * Area * NoL * LNoL / LightDistanceSquared;
}
